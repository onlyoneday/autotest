
*** Settings ***

Documentation   A test suite with a single test for invalid register.
Resource        resource.txt
Suite Setup     Open Browser To Register Page
Test Setup      Go To Register Page
Test Template   Register With Invalid Info Should Fail
Suite Teardown  Close Browser


*** Variables ***
${VALID EMAIL}   test@test.com

*** Test Cases ***    email             company            weixin             phone num            password            repassword
Invalid Email         ASDF@             ${VALID COMPANY}   ${VALID WEIXIN}    ${VALID PHONE NUM}   ${VALID PASSWORD}   ${VALID PASSWORD}
Empty Email           ${EMPTY}          ${VALID COMPANY}   ${VALID WEIXIN}    ${VALID PHONE NUM}   ${VALID PASSWORD}   ${VALID PASSWORD}
Empty Company         ${VALID EMAIL}    ${EMPTY}           ${VALID WEIXIN}    ${VALID PHONE NUM}   ${VALID PASSWORD}   ${VALID PASSWORD}
Empty Weixin          ${VALID EMAIL}    ${VALID COMPANY}   ${EMPTY}           ${VALID PHONE NUM}   ${VALID PASSWORD}   ${VALID PASSWORD}
Empty Password        ${VALID EMAIL}    ${VALID COMPANY}   ${VALID WEIXIN}    ${VALID PHONE NUM}   ${EMPTY}            ${VALID PASSWORD}   
Empty Repassword      ${VALID EMAIL}    ${VALID COMPANY}   ${VALID WEIXIN}    ${VALID PHONE NUM}   ${VALID PASSWORD}   ${EMPTY}
Invalid Password      ${VALID EMAIL}    ${VALID COMPANY}   ${VALID WEIXIN}    ${VALID PHONE NUM}   11111111            11111111
Password not equal    ${VALID EMAIL}    ${VALID COMPANY}   ${VALID WEIXIN}    ${VALID PHONE NUM}   ${VALID PASSWORD}   111111xX


*** Keywords ***

Register With Invalid Info Should Fail
    [Arguments]  ${email}  ${company}  ${weixin}  ${phone num}  ${password}  ${repassword}
    Input Email  ${email}
    Input Company  ${company}
    Input Weixin  ${weixin}
    Input Phone Number  ${phone num}
    Input Password  ${password}
    Input Password Again  ${repassword}
    Submit Register
    Register Success Page Should Not Be Open