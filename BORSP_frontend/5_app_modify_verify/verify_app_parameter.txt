*** Settings ***

Documentation   验证APP详细参数（Header、实例、配置、DEBUG）
Resource        ../common/resources.txt
Resource        ../common/app_resources.txt
Suite Setup     Open Browser And Login Mmj
Test Setup      Go To Page   ${APP_MY URL}
Suite Teardown  Close All Browsers
Library         DateTime

*** Test Cases ***

app_m_5-7,9-11,15-21,23 验证应用参数
    [Documentation]  实例链接没有跑
    #新建应用
    Click Element       stackCreate
    Wait Until Page Contains Element  stackAppName
    ${name} =           Input Simple App
    Input Text          stackImageAddress   ${image}
    Input Advanced App  11110
    #等待应用发布
    Click And Wait     stackFormCreate
    Wait Until Element Contains  ${APP STATUS PATH}  ${RUNNING}  timeout=${LONG TIME} 

    #验证信息是否正确
    Click Element      ${APP LINK PATH}
    Wait Until Page Contains Element  stackDetailMenu
    #应用名称
    ${app name} =      Get Text      ${nametext} 
    Should Contain     ${app name}   ${name}
    #验证Header
    Verify Header
    #验证实例
    Verify Instance
    #验证配置
    Verify Config

    #编辑按钮可用
    Click Element       stackConfigEditFirst
    Wait Until Page Contains Element  stackImageAddress
    #更新应用
    Modify Parameter
    #等待应用发布
    Click And Wait      stackFormUpdate

    #验证信息是否正确
    Click Element       ${APP LINK PATH}
    Wait Until Page Contains Element  stackDetailMenu
    #验证实例2
    Verify Config 2
    #验证Debug
    Reload Page
    Verify Debug

    #删除该应用（避免垃圾数据）
    Delete App From Detail

*** Keywords ***

Modify Parameter
    #更改基本参数
    Input Text        stackImageAddress   ${wrong image}
    Click Element     forceImage
    Click Element     stackHost
    Input Text        stackCpus      0.1
    Clear Element Text   stackMemory
    Input Text        stackMemory    64
    Input Text        stackInstanceNumber   2
    #更改高级设置
    Click Element     ${APP ADVENTURE}
    Sleep  1
    #环境变量
    Input Text        stackEnvsValue0     ${inputevm}
    #Docker参数
    Input Text        stackDockerValue-0  ${inputdocker}
    #健康检查
    Input Text        stackMaxConsecutiveFailures-0  6

Verify Header
    #Header-更新时间
    ${cur date} =     Get Current Date
    ${app date} =     Get Text    ${statustext}
    ${cd} =   Get Substring   ${cur date}   0  13
    ${ad} =   Get Substring   ${app date}   0  13
    #Should Be Equal As Strings   ${cd}   ${ad}
    #Header-应用状态
    ${app status} =   Get Text   ${statustext}
    Should Be Equal As Strings   ${app status}    ${RUNNING}
    #Healthy
    ${app healthy} =  Get Text   ${healthytext}
    Should Be Equal As Strings   ${app healthy}   ${HEALTHY}

Verify Instance
    #实例-实例数
    ${index} =      Get Matching Xpath Count   //*[@id="stackInstanceList"]/tbody/tr[*] 
    Should Be Equal As Strings   ${index}  1
    #实例-链接可用
    #Click And Wait  xpath=//*[@id='stackInstanceList']/tbody/tr/td[2]/p/span/a
    #Page Should Contain   Welcome to nginx!
    #Go Back And Wait

Verify Config
    #转到配置tab
    Click And Wait  ${APP TAB 2}
    #CPU
    ${cpu} =    Get Text   ${cputext}
    Should Be Equal As Strings   ${cpu}    0.2
    #内存
    ${mem} =    Get Text   ${memtext}
    Should Be Equal As Strings   ${mem}    32.00 MB
    #实例个数
    ${num} =    Get Text   ${numtext}
    Should Be Equal As Strings   ${num}    1
    #CMD
    ${cmd} =   Get Text    ${cmdtext}
    Should Be Equal As Strings   ${cmd}    -
    #镜像
    Page Should Contain    "image": "nginx:latest"
    Page Should Contain    "forcePullImage": false
    #网络模式
    Page Should Contain    "network": "BRIDGE"
    #环境变量
    Page Should Contain    ${inputenvironment}
    #Docker参数
    Page Should Contain    ${VALID USER}
    #健康检查
    Page Should Contain    "maxConsecutiveFailures": 5

Verify Config 2
    #转到配置tab
    Click And Wait  ${APP TAB 2}
    #CPU
    ${cpu} =    Get Text   ${cputext}
    Should Be Equal As Strings   ${cpu}    0.1
    #内存
    ${mem} =    Get Text   ${memtext}
    Should Be Equal As Strings   ${mem}    64.00 MB
    #实例个数
    ${num} =    Get Text   ${numtext}
    Should Be Equal As Strings   ${num}    2
    #版本信息（原、新）
    Click And Wait  stackConfigDetail0
    #镜像
    Page Should Contain    "image": "nginx:latest"
    Page Should Contain    "forcePullImage": false
    Page Should Contain    "image": "nginx:latestt"
    Page Should Contain    "forcePullImage": true
    #网络模式
    Page Should Contain    "network": "BRIDGE"
    Page Should Contain    "network": "HOST"
    #环境变量
    Page Should Contain    ${inputevm} 
    Page Should Contain    ${inputenvironment}
    #Docker参数
    Page Should Contain    ${VALID USER}
    Page Should Contain    ${inputdocker}
    #健康检查
    Page Should Contain    "maxConsecutiveFailures": 5
    Page Should Contain    "maxConsecutiveFailures": 6

Verify Debug
    #Debug信息
    Click And Wait  ${APP TAB 3}
    Page Should Contain    ${debugmessage}

*** Variables ***
${image}                 nginx:latest
${wrong image}           nginx:latestt
${nametext}              css=h2
${statustext}            css=dl:nth-child(1) > dd
${cputext}               css=div.flex-75 > div > dl:nth-child(2) > dd
${memtext}               css=div.flex-75 > div > dl:nth-child(3) > dd
${numtext}               css=div.flex-75 > div > dl:nth-child(4) > dd
${cmdtext}               css=div.flex-75 > div > dl:nth-child(5) > dd
${healthytext}           //*[@id='stackInstanceList']/tbody/tr[1]/td[3]
${debugmessage}          docker -H unix:///var/run/docker.sock pull nginx:latestt
${inputdocker}           docker
${inputevm}              evm
${inputenvironment}      environment       


