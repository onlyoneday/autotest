*** Settings ***

Documentation   登录（异常）
Resource        ../common/resources.txt
Suite Setup     Open Browser To Login Page
Test Setup      Go To Page  ${LOGIN URL}
Test Template   Invalid Login
Suite Teardown  Close All Browsers

*** Test Cases ***      User Name            Password            Info

user_4                  admin@dataman.com    ${VALID PASSWD}     ${no record}
user_4-2                ${VALID USER}        invalid             ${no record}
user_4-3                invalid              whatever            ${no record}
user_5                  ${EMPTY}             ${VALID PASSWD}     ${EMPTY} 
user_5-2                ${VALID USER}        ${EMPTY}            ${EMPTY} 
user_5-3                ${EMPTY}             ${EMPTY}            ${EMPTY} 
user_6                  whatever             ${VALID PASSWD}     ${no record}

*** Keywords ***

Invalid Login
    [Arguments]  ${username}  ${password}  ${info}
    Input Text   ${USERNAME FIELD}   ${username}
    Input Text   ${PASSWORD FIELD}   ${password}
    Click Button     ${LOGIN BTN}
    Page Should Contain  ${info}
    Location Should Be   ${LOGIN URL}

*** Variables ***

${no record}    record not found
