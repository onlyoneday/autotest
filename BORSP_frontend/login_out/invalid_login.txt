*** Settings ***

Documentation   登录（异常）
Resource        ../common/resources.txt
Suite Setup     Open Browser To Login Page
Test Setup      Go To Page  ${LOGIN URL}
Test Template   Invalid Login
Suite Teardown  Close All Browsers

*** Test Cases ***      User Name          Password            Info

user_4-1用名错误        whatever           ${VALID PASSWD}     ${no record}
user_4-2密码错误        ${VALID USER}      invalid             ${no record}
user_4-3两个错误        invalid            whatever            ${no record}
user_5-1用名不填        ${EMPTY}           ${VALID PASSWD}     ${EMPTY} 
user_5-2密码不填        ${VALID USER}      ${EMPTY}            ${EMPTY} 
user_5-3两个不填        ${EMPTY}           ${EMPTY}            ${EMPTY} 

*** Keywords ***

Invalid Login
    [Arguments]  ${username}  ${password}  ${info}
    Input Text   ${USERNAME FIELD}   ${username}
    Input Text   ${PASSWORD FIELD}   ${password}
    Click Button     ${LOGIN BTN}
    Page Should Contain  ${info}
    Location Should Be   ${LOGIN URL}

*** Variables ***

${no record}     not correct
